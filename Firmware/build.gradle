apply plugin: 'c'
apply plugin: 'cpp'

defaultTasks 'opq'

model {
    platforms {
        arm32 {
            architecture "arm32"
        }
    }

    toolChains {
        crossGcc(Gcc) {
            target 'arm32'

            eachPlatform { tools ->
                tools.cCompiler.executable = 'arm-none-eabi-gcc'
                tools.cppCompiler.executable = 'arm-none-eabi-gcc'
                tools.assembler.executable = 'arm-none-eabi-gcc'
                tools.linker.executable = 'arm-none-eabi-gcc'
            }
        }
    }

    components {
        cmsis(NativeLibrarySpec) {
            sources {
                c.exportedHeaders {
                    srcDirs 'src/cmsis/headers'
                }
            }
        }

        cmsis_boot(NativeLibrarySpec) {
            sources {
                c.lib library: 'cmsis', linkage: 'static'
                c.lib library: 'stm_lib', linkage: 'static'

                c.exportedHeaders {
                    srcDirs 'src/cmsis_boot/headers'
                }
            }
        }

        stm_lib(NativeLibrarySpec) {
            sources {
                c.lib library: 'cmsis', linkage: 'static'
                c.lib library: 'cmsis_boot', linkage: 'static'

                c.exportedHeaders {
                    srcDirs 'src/stm_lib/headers'
                }
            }
        }

        opq(NativeExecutableSpec) {
            sources {
                cpp.lib library: 'cmsis', linkage: 'static'     
                cpp.lib library: 'cmsis_boot', linkage: 'static'     
                cpp.lib library: 'stm_lib', linkage: 'static'     
            }
            binaries.all {
                cCompiler.args '-flto'
                cppCompiler.args '-flto'
            } 
        }

        all {
            targetPlatform 'arm32'

            binaries.all { 
                def common = [
                    '-mcpu=cortex-m4', 
                    '-g',
                    '-mthumb',
                    '-mfpu=fpv4-sp-d16',
                    '-mfloat-abi=hard',
                    '-DSTM32F303VC', 
                    '-DSTM32F30X', 
                    '-D__FPU_USED',
                    '-DUSE_STDPERIPH_DRIVER',
                    '-ffunction-sections', '-fdata-sections',
                    '-Wl,--gc-sections'
                ]

                assembler.args(*common)
                cCompiler.args(*common)
                cppCompiler.args(*common, '--std=c++14')

                linker.args(*common, 
                    '-Tarm-gcc-link.ld', 
                    '--specs=nosys.specs', '-lnosys',
                    '-lc_nano', '-lg_nano', '-lstdc++_nano',
                )
           }
        }
    }
}
